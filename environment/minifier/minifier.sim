#this is out
/* 
	Copyright (c) 2018 Azeez Adewale <azeezadewale98@gmail.com> 
	MIT License Copyright (c) 2018 simple-lang
*/

/* 
 * @Filename - minifier.sim
 * @Author - Azeez Adewale
 * @Date - 09 August 2018
 * @Time - 10:55 PM
 */

call simple.io.File
call simple.debugging.Throw
call simple.core.String
call simple.utilities.Type

skipRequest = false
 
block main()
	sourceFile = null
	sourceFilePath = null
	sourceFileContent = null
	cmdparamsLen = lengthOf(cmdparams) - 1
	for a = 0 to cmdparamsLen {
		if cmdparams[a] == "-s" || cmdparams[a] == "--s" || cmdparams[a] == "--source" { sourceFilePath = cmdparams[a++]
		elif cmdparams[a] == "-y" || cmdparams[a] == "--yes" skipRequest = true
		else }
	}
	if sourceFilePath != null {
		sourceFile = new Path(sourceFilePath)
		if sourceFile.isADirectory() {
			minifySourceDir(sourceFile.toDirectory())
		elif sourceFile.isAFile()
			minifySourceFile(sourceFile.toFile())
		}
		__exit__
		if sourceFile.exists() {
			
		else
			throw(sourceFile.absolutePath()+" : does not exists")
		}
		
	}
	
block minifySourceDir(sourceDir)
	files = sourceDir.listFiles()
	filesLen = lengthOf(files) - 1
	for b = 0 to filesLen {
		minifySourceFile(files[b])
	}
	dirs = sourceDir.listDirectories()
	dirsLen = lengthOf(dirs) - 1
	for b = 0 to dirsLen {
		@"moving into directory : "+dirs[b].absolutePath()
		minifySourceDir(dirs[b])
	}
	
block minifySourceFile(sourceFile)
	if !strEndsWith(sourceFile.name(),".sim") {
		@sourceFile.name()+" : not a simple source file (*.sim) skipping..."
		return
	}
	if !skipRequest {
		display sourceFile.name()+" : do you want to minify this file? " read res
		if res != "y" and res != "yes" {
			return
		}
	}
	source = " "+sourceFile.readFileAsString() + " "
	if strGetLineCount(source) < 1 {
		@sourceFile.name()+" : already minified"
		return
	}
	display sourceFile.name()+" : minification starting..."
	minifiedSource = ""
	TOKEN_IDENTIFIER =  0 TOKEN_MULTILINE_COMMENT = 1 TOKEN_LITERAL_1 = 2 TOKEN_LITERAL_2 = 3
	type = TOKEN_IDENTIFIER
	sourceLen = lengthOf(source) - 2
	for i = 0 to sourceLen {
		switch type {
			case TOKEN_IDENTIFIER
				if source[i] == tab or isNewLine(source[i]) {
					if isWhiteSpace(source[i+1]) or (isPunctuation(source[i+1]) and source[i+1] != '_' and source[i+1] != '$' ) {
						minifiedSource += "" 
					else
						minifiedSource += " " 
					}
				elif source[i] == '/' and source[i+1] == '*' i++ type = TOKEN_MULTILINE_COMMENT
				elif source[i] == '"'  minifiedSource+= '"' type = TOKEN_LITERAL_1
				elif source[i] == "'"  minifiedSource+= "'" type = TOKEN_LITERAL_2
				elif source[i] == "#" and source[i+1] != '"' 
					while source[i+1] != cr and source[i+1] != nl and source[i+1] != crlf {
						if i < sourceLen {
							i++
						else 
							
						}
					}
				elif isPunctuation(source[i]) and (source[i] != "/" and source[i+1] != "*")
					if isWhiteSpace(source[i+1]) {
						minifiedSource += source[i]
						i++
					else
						minifiedSource += source[i]
					}
				else  minifiedSource+= source[i]
				}
			case TOKEN_MULTILINE_COMMENT
				if source[i] == '*' and source[i+1] == '/' and (source[i+2] != '"' or source[i+2] != "'") {
					i++ type = TOKEN_IDENTIFIER
				else 
					
				}
			case TOKEN_LITERAL_1
				minifiedSource+= source[i]
				if source[i] == '"' {
					type = TOKEN_IDENTIFIER
				}
			case TOKEN_LITERAL_2
				minifiedSource+= source[i]
				if source[i] == "'" {
					type = TOKEN_IDENTIFIER
				}
			default
				minifiedSource+= source[i]		
		}
	}
	__writefile(sourceFile.absolutePath(), minifiedSource)
	@" (done)"
	
block isNewLine(val)
	return val == crlf or val = nl or val == cr
/* 
	Copyright (c) 2018 Azeez Adewale <azeezadewale98@gmail.com> 
	MIT License Copyright (c) 2018 simple-lang
*/

/* 
 * @Filename - SimpleX.sim
 * @Author - Azeez Adewale
 * @Date - 23 July 2018
 * @Time - 01:11 PM
 */

/*
	
*/

call simple.system.loadsystemdll
call simple.system.System
call simple.io.Path

module simple.system

/*
	Get the Directory that the simple distribution are installed into, this is 
	different from the directory where the simple executables e.g simple.exe are. 
	The directory is where different version of simple-lang are installed. 
	A typical Heirachy example:
	@Code
		#Windows 
		C:\Simple\ #-> the simple directory
			.\s0.3.36\
			.\s0.3.36\
			.\s0.4.00\
		#linux
		/var/lib/simple/ #-> the simple directory
			.\s0.3.36\
			.\s0.3.36\
			.\s0.4.00\
	@EndCode
	@Return the simple-lang installation directory @DataType String
*/
block simpleDir()
	simpleDir = getSystemEnvironment("SIMPLE_PATH")
	if simpleDir == "(null)" || simpleDir == "" {
		simpleDir = exeFolder()
		if isWindows() { 
			if __dir_exists(simpleDir) { 
				simpleDir = removeLastChar(simpleDir)
				toRemove = lastStrAfterChar(simpleDir,"\") 
				simpleDir = removeStr(simpleDir,toRemove)  
			}
			if simpleDir == "(null)" || simpleDir == "" {
				simpleDir = "C:\Simple\"
			}
		else
			xPrefix = getEnv("DESTDIR")+"/"+getEnv("PREFIX")
			if getEnv("PREFIX") == "/" {
				xPrefix = "/usr/local"
			}
			tempPath = "/var/lib/simple/"
			simpleDir = tempPath
		}
	} 
	return simpleDir

/*
	Get the directory where simple-lang modules are installed. The location depends on 
	the operating system path
	@Return the simple-lang modules directory @DatatType String
*/
block simpleModulesDir()
	modulesDir = getEnv("SIMPLE_MODULE_PATH")
	if modulesDir == "" {
		modulesDir = simpleDir()+"s"+getSimpleVersion()+"/modules/"
		modulesDir = getCorrectPath(modulesDir)
	}
	return modulesDir

/*
	Get the directory where simple-lang dynamic modules are installed. The location depends on 
	the operating system path
	@Return the simple-lang dynamic modules directory @DatatType String
*/
block simpleDyModulesDir()
	modulesDir = getEnv("SIMPLE_MODULE_PATH")
	if modulesDir == "" {
		dyModulesDir = simpleDir()+"s"+getSimpleVersion()+"/modules/dynamic_modules/"
	else
		dyModulesDir = modulesDir+"/dynamic_modules/"
		dyModulesDir = getCorrectPath(dyModulesDir)
	}
	return dyModulesDir

/*
	Get the directory where simple-lang dynamic modules are installed. The location depends on 
	the operating system path
	@Return the simple-lang dynamic modules directory @DatatType String
*/
block simpleDocDir()
	modulesDir = getEnv("SIMPLE_MODULE_PATH")
	if modulesDir == "" {
		docDir = simpleDir()+"s"+getSimpleVersion()+"/docs/"
	else
		docDir = modulesDir+"/../docs/"
	}
	return getCorrectPath(docDir)

/*
	Get the directory where the simple executable directory 
	@Return where simple-lang executable directory @DataType String
*/
block simpleExecDir()
	simpleExecDir = exeFolder()
	if !__exists(simpleExecDir+"/simple"+simpleXExeExt()) {
		if isWindows() {
			simpleExecDir = simpleDir()+"/s"+getSimpleVersion()+"/bin/"
		else
			simpleExecDir = getEnv("DESTDIR")+"/"+getEnv("PREFIX")+"/bin/"
		}
	}
	return simpleExecDir

/*
	Get the extension type for the specific operating system
	@Return the system executable directory @DataType String
*/
block simpleXExeExt()
	if isWindows() {
		return ".exe"
	}
	return ""

/*
	Get the dynamic modules extension for the specific operating system
	@Return the system dynamic module extension @DataType String
*/
block dynamicModuleExt()
	if isWindows() {
		return "dll"
	elif isMacosx()
		return "dylib"
	else
		return "so"
	}
	return ""
	
	
/*

*/
block simpleIncludesDir()
	includesDir = simpleDir()+"/includes/"
	return getCorrectPath(includesDir)
	
	
